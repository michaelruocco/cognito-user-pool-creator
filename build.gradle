plugins {
    id "java-library"
    id "jacoco"
    id "maven-publish"
    id "signing"
    id "java-test-fixtures"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "com.diffplug.spotless" version "6.25.0"
    id "com.github.ben-manes.versions" version "0.51.0"
    id "pl.allegro.tech.build.axion-release" version "1.17.2"
    id "org.sonarqube" version "5.0.0.4638"
    id "nebula.lint" version "19.0.2"
}

group "com.github.michaelruocco"

ext {
    slf4jVersion = "2.0.13"
    lombokVersion = "1.18.32"
    cognitoVersion = "2.26.0"

    junitVersion = "5.10.2"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "${name}-"
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

gradleLint {
    rules = ['all-dependency']
    alwaysRun = false
}

dependencies {
    testImplementation project(path: ':')
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "software.amazon.awssdk:cognitoidentityprovider:${cognitoVersion}"

    testFixturesCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testFixturesAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testFixturesImplementation "org.testcontainers:postgresql:1.19.8"
    testFixturesImplementation "software.amazon.awssdk:cognitoidentityprovider:${cognitoVersion}"
    testFixturesImplementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testFixturesImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

testing {
    suites {
        withType(JvmTestSuite) {
            useJUnitJupiter(junitVersion)
            dependencies {
                runtimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
                runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
                compileOnly "org.projectlombok:lombok:${lombokVersion}"
                annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
                implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
                implementation "org.assertj:assertj-core:3.26.0"
                implementation "org.mockito:mockito-core:5.12.0"
            }
            targets {
                configureEach {
                    testTask.configure {
                        systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
                        testLogging {
                            events = ["passed", "failed", "skipped"]
                            showStandardStreams = true
                            exceptionFormat = "full"
                        }
                    }
                }
            }
        }
        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            dependencies {
                implementation project()
            }
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
    }
}

project.tasks["sonar"].dependsOn jacocoTestReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_${name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/report.xml"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def gitUrl = "https://github.com/michaelruocco/cognito-user-pool-creator"
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "mruoc"
            name "Michael Ruocco"
            email "michael.ruocco@hotmail.com"
        }
    }

    scm {
        url gitUrl
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}

publishing {
    publications {
        lib(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Template repo to speed up creating new library projects")
                root.appendNode("name", project.name)
                root.appendNode("url", gitUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

signing {
    def signingKey = System.getenv("OSSRH_PGP_SECRET_KEY")
    def signingPassword = System.getenv("OSSRH_PGP_SECRET_KEY_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.lib
}
signLibPublication.enabled = gradle.startParameter.taskNames.contains("publishToSonatype")

spotless {
    java {
        palantirJavaFormat("2.47.0")
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
